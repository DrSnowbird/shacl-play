PREFIX sh:   <http://www.w3.org/ns/shacl#>

SELECT ?sourceShape ?sourceConstraintComponent ?resultSeverity ?resultPath ?message (COUNT(?nodeUri) AS ?count) (SAMPLE(?focusNode) AS ?sampleFocusNode) (SAMPLE(?value) AS ?sampleValue)
WHERE {
	?s a sh:ValidationResult;
		sh:focusNode ?nodeUri;
		sh:resultSeverity ?resultSeverity;
		sh:sourceConstraintComponent ?sourceConstraintComponent;
		sh:sourceShape ?sourceShape .	
	
	# resultPath is not here on AndConstraintComponent
	# OPTIONAL { ?s sh:resultPath ?resultPath; }
	# we look on the source shape directly so that we can can aggregate on the same blank node
	# in case the sh:path is a SHACL property path
	OPTIONAL { ?sourceShape sh:path ?resultPath . }
	# result message is not here on AndConstraintComponent
	OPTIONAL { ?s sh:resultMessage ?message; }
	
	?s sh:focusNode ?focusNode .
	# value is not present for min cardinality violations
	OPTIONAL { ?s sh:value ?value. }
	
}
GROUP BY ?sourceShape ?sourceConstraintComponent ?resultSeverity ?resultPath ?message
ORDER BY DESC(?count)